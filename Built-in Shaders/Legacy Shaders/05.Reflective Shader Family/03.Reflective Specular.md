# Reflective Specular (Reflect-Glossy.shader)
* <https://docs.unity3d.com/Manual/shader-ReflectiveSpecular.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
Shader "Legacy Shaders/Reflective/Specular" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    _SpecColor ("Specular Color", Color) = (0.5, 0.5, 0.5, 1)
    // [属性描绘器], 参考[Material Property Drawer]
    // 这里指 _Shininess^5 在(0.01, 1)范围内
    [PowerSlider(5.0)] _Shininess ("Shininess", Range (0.01, 1)) = 0.078125
    _ReflectColor ("Reflection Color", Color) = (1,1,1,0.5)
    _MainTex ("Base (RGB) Gloss (A)", 2D) = "white" {}
    _Cube ("Reflection Cubemap", Cube) = "_Skybox" {}
}

// SubShader, 必须, 参考[Syntax]
SubShader {
    // 参考[LOD], 值越小适用范围越广
    LOD 300
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }

CGPROGRAM
// #pragma surface surfaceFunction lightModel [optionalparams]
// 指明表面着色器函数名 surf, 指明光照模型 BlinnPhong
#pragma surface surf BlinnPhong

// 对应Properties中定义的属性
sampler2D _MainTex;
samplerCUBE _Cube;

fixed4 _Color;
fixed4 _ReflectColor;
half _Shininess;

struct Input {
    float2 uv_MainTex;
    float3 worldRefl;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
    fixed4 c = tex * _Color;
    o.Albedo = c.rgb;
    o.Gloss = tex.a;
    o.Specular = _Shininess;

    fixed4 reflcol = texCUBE (_Cube, IN.worldRefl);
    reflcol *= tex.a;
    o.Emission = reflcol.rgb * _ReflectColor.rgb;
    o.Alpha = reflcol.a * _ReflectColor.a;
}
ENDCG
}

// 参考[Fallback]
FallBack "Legacy Shaders/Reflective/VertexLit"
}

```

## Lighting.cginc
可以看到BlinnPhong光照模型的计算

```HLSL
inline fixed4 UnityBlinnPhongLight (SurfaceOutput s, half3 viewDir, UnityLight light)
{
    // 计算half direction
    half3 h = normalize (light.dir + viewDir);
    // 计算漫反射系数
    fixed diff = max (0, dot (s.Normal, light.dir));

    // 计算nh
    float nh = max (0, dot (s.Normal, h));
    // Specular乘128处理, 最后乘Gloss
    float spec = pow (nh, s.Specular*128.0) * s.Gloss;

    fixed4 c;
    // 最后使用漫反射光和镜面反射光
    c.rgb = s.Albedo * light.color * diff + light.color * _SpecColor.rgb * spec;
    c.a = s.Alpha;

    return c;
}
```

* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
