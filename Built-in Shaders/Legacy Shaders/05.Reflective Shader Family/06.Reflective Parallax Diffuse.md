# Reflective Parallax Diffuse (Reflect-Parallax.shader)
* <https://docs.unity3d.com/Manual/shader-ReflectiveParallaxDiffuse.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// 视差效果参考[Parallax]

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
Shader "Legacy Shaders/Reflective/Parallax Diffuse" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    _ReflectColor ("Reflection Color", Color) = (1,1,1,0.5)
    _Parallax ("Height", Range (0.005, 0.08)) = 0.02
    _MainTex ("Base (RGB) RefStrength (A)", 2D) = "white" {}
    _Cube ("Reflection Cubemap", Cube) = "_Skybox" {}
    _BumpMap ("Normalmap", 2D) = "bump" {}
    _ParallaxMap ("Heightmap (A)", 2D) = "black" {}
}

// SubShader, 必须, 参考[Syntax]
SubShader {
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }
    // 参考[LOD], 值越小适用范围越广
    LOD 500

CGPROGRAM
// #pragma surface surfaceFunction lightModel [optionalparams]
// 指明表面着色器函数名 surf, 指明光照模型 Lambert
#pragma surface surf Lambert
// 参考[Target], 要求Shader Model 3.0以上
#pragma target 3.0

// 对应Properties中定义的属性
sampler2D _MainTex;
sampler2D _BumpMap;
samplerCUBE _Cube;
sampler2D _ParallaxMap;

fixed4 _Color;
fixed4 _ReflectColor;
float _Parallax;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
    float2 uv_BumpMap;
    // 参考[InternalData], 这里需要传入切线空间到世界空间的转换矩阵
    float3 worldRefl;
    float3 viewDir;
    INTERNAL_DATA
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    half h = tex2D (_ParallaxMap, IN.uv_BumpMap).w;
    float2 offset = ParallaxOffset (h, _Parallax, IN.viewDir);
    IN.uv_MainTex += offset;
    IN.uv_BumpMap += offset;

    fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
    fixed4 c = tex * _Color;
    o.Albedo = c.rgb;

    o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));

    // 参考[InternalData], 这里计算世界空间下的反射向量
    float3 worldRefl = WorldReflectionVector (IN, o.Normal);
    fixed4 reflcol = texCUBE (_Cube, worldRefl);
    reflcol *= tex.a;
    o.Emission = reflcol.rgb * _ReflectColor.rgb;
    o.Alpha = reflcol.a * _ReflectColor.a;
}
ENDCG
}

// 参考[Fallback]
FallBack "Legacy Shaders/Reflective/Bumped Diffuse"
}

```

## Lighting.cginc
可以看到Lambert光照模型的计算

```HLSL
inline fixed4 UnityLambertLight (SurfaceOutput s, UnityLight light)
{
    fixed diff = max (0, dot (s.Normal, light.dir));

    fixed4 c;
    c.rgb = s.Albedo * light.color * diff;
    c.a = s.Alpha;
    return c;
}
```

