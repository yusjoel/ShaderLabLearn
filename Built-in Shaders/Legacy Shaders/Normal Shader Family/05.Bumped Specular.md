# Bumped Specular (Normal-BumpSpec.shader)

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

Shader "Legacy Shaders/Bumped Specular" {
Properties {
    _Color ("Main Color", Color) = (1,1,1,1)
    _SpecColor ("Specular Color", Color) = (0.5, 0.5, 0.5, 1)
    _Shininess ("Shininess", Range (0.03, 1)) = 0.078125
    _MainTex ("Base (RGB) Gloss (A)", 2D) = "white" {}
    _BumpMap ("Normalmap", 2D) = "bump" {}
}

// CGINCLUDE这个语法在官网上没有找到具体的说明
// 这里猜想是可以插入到下面的两个SubShader中
CGINCLUDE
sampler2D _MainTex;
sampler2D _BumpMap;
fixed4 _Color;
half _Shininess;

struct Input {
    float2 uv_MainTex;
    float2 uv_BumpMap;
};

void surf (Input IN, inout SurfaceOutput o) {
    fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
    o.Albedo = tex.rgb * _Color.rgb;
    o.Gloss = tex.a;
    o.Alpha = tex.a * _Color.a;
    o.Specular = _Shininess;
    o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
}
ENDCG

SubShader { 
    Tags { "RenderType"="Opaque" }
    LOD 400
    
    CGPROGRAM
    #pragma surface surf BlinnPhong
    // [1] Code generation options
    // #pragma target name - which shader target to compile to.
    #pragma target 3.0
    ENDCG
}

SubShader { 
    Tags { "RenderType"="Opaque" }
    LOD 400
    
    CGPROGRAM
    // [2] Code generation options
    // nodynlightmap - Disables runtime dynamic global illumination support in this shader.
    #pragma surface surf BlinnPhong nodynlightmap
    ENDCG
}

FallBack "Legacy Shaders/Specular"
}


```


* <https://docs.unity3d.com/Manual/shader-NormalBumpedSpecular.html>
* [1] Shader Compilation Targets <https://docs.unity3d.com/Manual/SL-ShaderCompileTargets.html>
* [2] Code generation options <https://docs.unity3d.com/Manual/SL-SurfaceShaders.html>