# Self-Illuminated Normal mapped Diffuse (Illumin-Bumped.shader)
* <https://docs.unity3d.com/Manual/shader-SelfIllumBumpedDiffuse.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
Shader "Legacy Shaders/Self-Illumin/Bumped Diffuse" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    _MainTex ("Base (RGB)", 2D) = "white" {}
    _Illum ("Illumin (A)", 2D) = "white" {}
    _BumpMap ("Normalmap", 2D) = "bump" {}
    _Emission ("Emission (Lightmapper)", Float) = 1.0
}

CGINCLUDE

// 对应Properties中定义的属性
sampler2D _MainTex;
sampler2D _BumpMap;
sampler2D _Illum;
fixed4 _Color;
fixed _Emission;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
    float2 uv_Illum;
    float2 uv_BumpMap;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
    fixed4 c = tex * _Color;
    o.Albedo = c.rgb;
    o.Emission = c.rgb * tex2D(_Illum, IN.uv_Illum).a;
#if defined (UNITY_PASS_META)
    o.Emission *= _Emission.rrr;
#endif
    o.Alpha = c.a;
    o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
}
ENDCG

// SubShader, 必须
SubShader {
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }
    // 参考[LOD], 值越小适用范围越广
    LOD 300

    CGPROGRAM
    // #pragma surface surfaceFunction lightModel [optionalparams]
    // 指明表面着色器函数名 surf, 指明光照模型 Lambert
    #pragma surface surf Lambert
    // 参考[Target], 要求Shader Model 3.0以上
    #pragma target 3.0
    ENDCG
}

// 如果Shader Model 3.0以下, 则关闭实时动态GI
SubShader {
    Tags { "RenderType"="Opaque" }
    LOD 300

    CGPROGRAM
    // 参考[SurfaceShader]
    // Code generation options
    // nodynlightmap - Disables runtime dynamic global illumination support in this shader.
    #pragma surface surf Lambert nodynlightmap
    ENDCG
}

// 参考[Fallback]
FallBack "Legacy Shaders/Self-Illumin/Diffuse"
CustomEditor "LegacyIlluminShaderGUI"

}

```

## Lighting.cginc
可以看到Lambert光照模型的计算

```HLSL
inline fixed4 UnityLambertLight (SurfaceOutput s, UnityLight light)
{
    fixed diff = max (0, dot (s.Normal, light.dir));

    fixed4 c;
    c.rgb = s.Albedo * light.color * diff;
    c.a = s.Alpha;
    return c;
}
```

* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)

