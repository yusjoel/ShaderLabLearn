# Self-Illuminated Vertex-Lit (Illumin-VertexLit.shader)
* <https://docs.unity3d.com/Manual/shader-SelfIllumVertexLit.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
Shader "Legacy Shaders/Self-Illumin/VertexLit" {
    
// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    _SpecColor ("Spec Color", Color) = (1,1,1,1)
    // [属性描绘器], 参考[Material Property Drawer]
    // 这里指 _Shininess^5 在(0.1, 1)范围内
    [PowerSlider(5.0)] _Shininess ("Shininess", Range (0.1, 1)) = 0.7
    _MainTex ("Base (RGB)", 2D) = "white" {}
    // 和Normal Shader相比, 多了_Illum和_Emission
    _Illum ("Illumin (A)", 2D) = "white" {}
    _Emission ("Emission (Lightmapper)", Float) = 1.0
}

// SubShader, 必须, 参考[Syntax]
SubShader {
    // 参考[LOD], 值越小适用范围越广
    LOD 100
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }

    // 参考[Syntax]
    Pass {
        // Pass名, 参考[Pass]
        Name "BASE"
        // Tags, 参考[Pass Tags]
        // LightMode, 定义渲染方式
        // Vertex: 无光照映射的顶点光
        Tags {"LightMode" = "Vertex"}
	
        // 参考[Legacy Lighting]
        Material {
            Diffuse [_Color]
            Shininess [_Shininess]
            Specular [_SpecColor]
        }
        // 启用灯光
        Lighting On
        // 参考[Legacy Texture Combiners]
        SetTexture [_Illum] {
            constantColor [_Color]
            combine constant lerp (texture) previous
        }
        SetTexture [_MainTex] {
            constantColor (1,1,1,1)SetTexture.
            Combine texture * previous, constant // UNITY_OPAQUE_ALPHA_FFP
        }
    }

    // 参考[Meta], 该Pass是为Lightmapper提供Albedo和Emission数据, 不参与实时渲染
    // Extracts information for lightmapping, GI (emission, albedo, ...)
    // This pass it not used during regular rendering.
    Pass
    {
        Name "META"
        Tags { "LightMode" = "Meta" }
        CGPROGRAM
        #pragma vertex vert
        #pragma fragment frag
        #pragma target 2.0
        #include "UnityCG.cginc"
        #include "UnityMetaPass.cginc"

        struct v2f
        {
            float4 pos : SV_POSITION;
            float2 uvMain : TEXCOORD0;
            float2 uvIllum : TEXCOORD1;
        #ifdef EDITOR_VISUALIZATION
            float2 vizUV : TEXCOORD2;
            float4 lightCoord : TEXCOORD3;
        #endif
            UNITY_VERTEX_OUTPUT_STEREO
        };

        float4 _MainTex_ST;
        float4 _Illum_ST;

        v2f vert (appdata_full v)
        {
            v2f o;
            UNITY_SETUP_INSTANCE_ID(v);
            UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);
            o.pos = UnityMetaVertexPosition(v.vertex, v.texcoord1.xy, v.texcoord2.xy, unity_LightmapST, unity_DynamicLightmapST);
            o.uvMain = TRANSFORM_TEX(v.texcoord, _MainTex);
            o.uvIllum = TRANSFORM_TEX(v.texcoord, _Illum);
        #ifdef EDITOR_VISUALIZATION
            o.vizUV = 0;
            o.lightCoord = 0;
            if (unity_VisualizationMode == EDITORVIZ_TEXTURE)
                o.vizUV = UnityMetaVizUV(unity_EditorViz_UVIndex, v.texcoord.xy, v.texcoord1.xy, v.texcoord2.xy, unity_EditorViz_Texture_ST);
            else if (unity_VisualizationMode == EDITORVIZ_SHOWLIGHTMASK)
            {
                o.vizUV = v.texcoord1.xy * unity_LightmapST.xy + unity_LightmapST.zw;
                o.lightCoord = mul(unity_EditorViz_WorldToLight, mul(unity_ObjectToWorld, float4(v.vertex.xyz, 1)));
            }
        #endif
            return o;
        }

        sampler2D _MainTex;
        sampler2D _Illum;
        fixed4 _Color;
        fixed _Emission;

        half4 frag (v2f i) : SV_Target
        {
            UnityMetaInput metaIN;
            UNITY_INITIALIZE_OUTPUT(UnityMetaInput, metaIN);

            fixed4 tex = tex2D(_MainTex, i.uvMain);
            fixed4 c = tex * _Color;
            metaIN.Albedo = c.rgb;
            metaIN.Emission = c.rgb * tex2D(_Illum, i.uvIllum).a;
        #if defined(EDITOR_VISUALIZATION)
            metaIN.VizUV = i.vizUV;
            metaIN.LightCoord = i.lightCoord;
        #endif

            return UnityMetaFragment(metaIN);
        }
        ENDCG
    }
}

Fallback "Legacy Shaders/VertexLit"
CustomEditor "LegacyIlluminShaderGUI"
}
```

* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Material Property Drawer](../../../ShaderLab%20Reference/Material%20Property%20Drawer.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
* [Pass](../../../ShaderLab%20Reference/ShaderLab%20Pass.md)
* [Pass Tags](../../../ShaderLab%20Reference/Pass%20Tags.md)
* [Legacy Lighting](../../../ShaderLab%20Reference/ShaderLab%20Legacy%20Lighting.md)
* [Legacy Texture Combiners](../../../ShaderLab%20Reference/ShaderLab%20Legacy%20Combiners.md)
* [Meta](../../../ShaderLab%20Reference/Shader%20Meta%20Pass.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)

