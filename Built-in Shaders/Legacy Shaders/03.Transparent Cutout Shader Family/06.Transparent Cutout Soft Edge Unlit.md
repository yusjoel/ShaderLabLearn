# Transparent Cutout Soft Edge Unlit (AlphaTest-SoftEdgeUnlit.shader)

Unity3D Documenttation中没有相关页面

// 无光照的双面渲染
// 一个Pass描绘不透明部分, 另一个Pass描绘半透部分

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

/*
Renders doubled sides objects without lighting. Useful for
grass, trees or foliage.

This shader renders two passes for all geometry, one
for opaque parts and one with semitransparent details.

This makes it possible to render transparent objects
like grass without them being sorted by depth.
*/

Shader "Legacy Shaders/Transparent/Cutout/Soft Edge Unlit" {
Properties {
	_Color ("Main Color", Color) = (1, 1, 1, 1)
	_MainTex ("Base (RGB) Alpha (A)", 2D) = "white" {}
	_Cutoff ("Base Alpha cutoff", Range (0,.9)) = .5
}

SubShader {
	Tags { "Queue"="AlphaTest" "IgnoreProjector"="True" "RenderType"="TransparentCutout" }
	// fixed-function, see [Legacy Lighting] https://docs.unity3d.com/Manual/SL-Material.html
	// 关闭顶点光照
	Lighting off
	
	// Render both front and back facing polygons.
	// Cull Back | Front | Off, 关闭剔除, 正反面都描绘
	Cull Off
	
	// first pass:
	//   render any pixels that are more than [_Cutoff] opaque
	// 第一步, 描绘不透明部分
	Pass {  
		CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			// 支持雾, 参考UnityCG.cginc <Fog helpers>
			#pragma multi_compile_fog

			#include "UnityCG.cginc"

			struct appdata_t {
				float4 vertex : POSITION;
				float4 color : COLOR;
				float2 texcoord : TEXCOORD0;
				// GPU instancing see <https://docs.unity3d.com/Manual/GPUInstancing.html>
				// 定义在UnityInstancing.cginc中
				UNITY_VERTEX_INPUT_INSTANCE_ID
			};

			struct v2f {
				float4 vertex : SV_POSITION;
				fixed4 color : COLOR;
				float2 texcoord : TEXCOORD0;
				UNITY_FOG_COORDS(1)
				UNITY_VERTEX_OUTPUT_STEREO
			};

			sampler2D _MainTex;
			float4 _MainTex_ST;
			fixed _Cutoff;
			
			v2f vert (appdata_t v)
			{
				v2f o;
				UNITY_SETUP_INSTANCE_ID(v);
				UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);
				o.vertex = UnityObjectToClipPos(v.vertex);
				o.color = v.color;
				o.texcoord = TRANSFORM_TEX(v.texcoord, _MainTex);
				UNITY_TRANSFER_FOG(o,o.vertex);
				return o;
			}
			
			fixed4 _Color;
			fixed4 frag (v2f i) : SV_Target
			{
				half4 col = _Color * tex2D(_MainTex, i.texcoord);
				clip(col.a - _Cutoff);
				UNITY_APPLY_FOG(i.fogCoord, col);
				return col;
			}
		ENDCG
	}

	// Second pass:
	//   render the semitransparent details.
	// 第二部: 描绘半透明部分
	Pass {
		// RequireOptions:
		// SoftVegetation: Render this pass only if Soft Vegetation is on in Quality Settings.
		Tags { "RequireOption" = "SoftVegetation" }
		
		// Dont write to the depth buffer
		// 关闭写入深度缓存
		ZWrite off
		
		// Set up alpha blending
		// 设置混色
		Blend SrcAlpha OneMinusSrcAlpha
		
		CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			#pragma multi_compile_fog
			
			#include "UnityCG.cginc"

			struct appdata_t {
				float4 vertex : POSITION;
				float4 color : COLOR;
				float2 texcoord : TEXCOORD0;
				UNITY_VERTEX_INPUT_INSTANCE_ID
			};

			struct v2f {
				float4 vertex : SV_POSITION;
				fixed4 color : COLOR;
				float2 texcoord : TEXCOORD0;
				UNITY_FOG_COORDS(1)
				UNITY_VERTEX_OUTPUT_STEREO
			};

			sampler2D _MainTex;
			float4 _MainTex_ST;
			float _Cutoff;
			
			v2f vert (appdata_t v)
			{
				v2f o;
				UNITY_SETUP_INSTANCE_ID(v);
				UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);
				o.vertex = UnityObjectToClipPos(v.vertex);
				o.color = v.color;
				o.texcoord = TRANSFORM_TEX(v.texcoord, _MainTex);
				UNITY_TRANSFER_FOG(o,o.vertex);
				return o;
			}
			
			fixed4 _Color;
			fixed4 frag (v2f i) : SV_Target
			{
				half4 col = _Color * tex2D(_MainTex, i.texcoord);
				// 描绘不透明部分
				clip(-(col.a - _Cutoff));
				UNITY_APPLY_FOG(i.fogCoord, col);
				return col;
			}
		ENDCG
	}
}

}

```

* 参考《Unity Shader入门精要》8.7 双面渲染的透明效果　


* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
* [Projector](../../../ShaderLab%20Reference/Projector.md)
* [Target](../../../ShaderLab%20Reference/Shader%20Compilation%20Target.md)
