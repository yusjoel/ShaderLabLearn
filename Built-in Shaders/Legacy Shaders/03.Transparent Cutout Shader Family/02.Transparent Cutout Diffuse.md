# Transparent Cutout Diffuse (AlphaTest-Diffuse.shader)
* <https://docs.unity3d.com/Manual/shader-TransCutDiffuse.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
Shader "Legacy Shaders/Transparent/Cutout/Diffuse" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    // 和Normal Diffuse相比, 纹理中多了Alpha通道
    _MainTex ("Base (RGB) Trans (A)", 2D) = "white" {}
    // 和Transparent Diffuse相比, 多了Cutoff属性
    _Cutoff ("Alpha cutoff", Range(0,1)) = 0.5
}

// SubShader, 必须
SubShader {
    // 参考[Tags]
    // Queue: 和描绘顺序有关
    // IgnoreProjector: 忽略[Projector], 对于透明材质没有好的处理方法
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags {"Queue"="AlphaTest" "IgnoreProjector"="True" "RenderType"="TransparentCutout"}
    // 参考[LOD], 值越小适用范围越广
    LOD 200

// 参考[SurfaceShader]
// 表面着色代码也是写在CGPROGRAM - ENDCG块中, 但是包含在SubShader, 而不是Pass中

CGPROGRAM
// #pragma surface surfaceFunction lightModel [optionalparams]
// 指明表面着色器函数名 surf, 指明光照模型 Lambert
// 内置了两个非物理光照模型 Lambert(散射) 和 BlinnPhong (镜面反射)
// alphatest:VariableName
#pragma surface surf Lambert alphatest:_Cutoff

// 对应Properties中定义的属性
sampler2D _MainTex;
fixed4 _Color;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    // 采样
    fixed4 c = tex2D(_MainTex, IN.uv_MainTex) * _Color;
    o.Albedo = c.rgb;
    o.Alpha = c.a;
}
ENDCG
}

// 参考[Fallback]
Fallback "Legacy Shaders/Transparent/Cutout/VertexLit"
}

```

## Lighting.cginc
可以看到Lambert散射光的计算

```HLSL
inline fixed4 UnityLambertLight (SurfaceOutput s, UnityLight light)
{
    fixed diff = max (0, dot (s.Normal, light.dir));

    fixed4 c;
    c.rgb = s.Albedo * light.color * diff;
    c.a = s.Alpha;
    return c;
}
```

* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
* [Projector](../../../ShaderLab%20Reference/Projector.md)
