# Vertex-Lit (Normal-VertexLit.shader)

最简单的一个Shader, 代码反而是最复杂的, 这个放到之后分析

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

Shader "Legacy Shaders/VertexLit" {
Properties {
	_Color ("Main Color", Color) = (1,1,1,1)
	_SpecColor ("Spec Color", Color) = (1,1,1,1)
	_Emission ("Emissive Color", Color) = (0,0,0,0)
	_Shininess ("Shininess", Range (0.01, 1)) = 0.7
	_MainTex ("Base (RGB)", 2D) = "white" {}
}

SubShader {
	Tags { "RenderType"="Opaque" }
	LOD 100
	
	// Non-lightmapped
	Pass {
		// [LightMode]
		// Vertex: Used in legacy Vertex Lit rendering when object is not lightmapped;
		// all vertex lights are applied.
		Tags { "LightMode" = "Vertex" }
		
		// [Legacy Lighting]
		//  Ambient * Lighting Window’s Ambient Intensity setting + (Light Color * Diffuse + Light Color * Specular) + Emission 
		Material {
			Diffuse [_Color]
			Ambient [_Color]
			Shininess [_Shininess]
			Specular [_SpecColor]
			Emission [_Emission]
		} 
		// 启用灯光, 如果设置为Off, 使用Color命令指定的颜色
		Lighting On
		// If you specify SeparateSpecular On in the pass options, the specular color will be added in after the combiner calculation, rather than before. 
		SeparateSpecular On
		// [Legacy Texture Combiners]
		// SetTexture [TextureName] {Texture Block}
		SetTexture [_MainTex] {
			// ConstantColor color: Defines a constant color that can be used in the combine command.
			constantColor (1,1,1,1)
			// Texture is the color of the texture specified by TextureName in the SetTexture (see above).
			// Primary is the color from the lighting calculation or the vertex color if it is bound.
			// Constant is the color specified in ConstantColor.
			// DOUBLE: The formulas specified above can optionally be followed by the keywords Double or Quad to make the resulting color 2x or 4x as bright.
			// 逗号后的参数是Alpha, 默认设为1
			Combine texture * primary DOUBLE, constant // UNITY_OPAQUE_ALPHA_FFP
		} 
	}
	
	// Lightmapped, encoded as dLDR
	Pass {
		// VertexLM: Used in legacy Vertex Lit rendering when object is lightmapped; on platforms where lightmap is double-LDR encoded (mobile platforms).
		Tags { "LightMode" = "VertexLM" }
		
		// [Legacy BindChannels]
		BindChannels {
			Bind "Vertex", vertex
			Bind "normal", normal
			Bind "texcoord1", texcoord0 // lightmap uses 2nd uv
			Bind "texcoord", texcoord1 // main uses 1st uv
		}
		
		SetTexture [unity_Lightmap] {
			matrix [unity_LightmapMatrix]
			constantColor [_Color]
			// Light Map * Main Color
			combine texture * constant
		}
		SetTexture [_MainTex] {
			constantColor (1,1,1,1)
			// Previous is the the result of the previous SetTexture.
			combine texture * previous DOUBLE, constant // UNITY_OPAQUE_ALPHA_FFP
		}
	}
	
	// Lightmapped, encoded as RGBM
	Pass {
		// VertexLMRGBM: Used in legacy Vertex Lit rendering when object is lightmapped; on platforms where lightmap is RGBM encoded (PC & console).
		Tags { "LightMode" = "VertexLMRGBM" }
		
		BindChannels {
			Bind "Vertex", vertex
			Bind "normal", normal
			Bind "texcoord1", texcoord0 // lightmap uses 2nd uv
			Bind "texcoord1", texcoord1 // unused
			Bind "texcoord", texcoord2 // main uses 1st uv
		}
		
		SetTexture [unity_Lightmap] {
			matrix [unity_LightmapMatrix]
			// All the src properties can be followed by alpha to take only the alpha channel.
			// 这个模式的Light Map是带有Alpha的
			combine texture * texture alpha DOUBLE
		}
		SetTexture [unity_Lightmap] {
			constantColor [_Color]
			combine previous * constant
		}
		SetTexture [_MainTex] {
			constantColor (1,1,1,1)
			// 和上一个模式区别在于这里*4
			combine texture * previous QUAD, constant // UNITY_OPAQUE_ALPHA_FFP
		}
	}
	
	// Pass to render object as a shadow caster
	Pass {
		Name "ShadowCaster"
		// ShadowCaster: Renders object depth into the shadowmap or a depth texture.
		Tags { "LightMode" = "ShadowCaster" }
		
CGPROGRAM
#pragma vertex vert
#pragma fragment frag
#pragma target 2.0
#pragma multi_compile_shadowcaster
#pragma multi_compile_instancing // allow instanced shadow pass for most of the shaders
#include "UnityCG.cginc"

struct v2f { 
	V2F_SHADOW_CASTER;
	UNITY_VERTEX_OUTPUT_STEREO
};

v2f vert( appdata_base v )
{
	v2f o;
	UNITY_SETUP_INSTANCE_ID(v);
	UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);
	TRANSFER_SHADOW_CASTER_NORMALOFFSET(o)
	return o;
}

float4 frag( v2f i ) : SV_Target
{
	SHADOW_CASTER_FRAGMENT(i)
}
ENDCG

	}
	
}

}
```

* <https://docs.unity3d.com/Manual/shader-NormalVertexLit.html>
* [LightMode](https://docs.unity3d.com/Manual/SL-PassTags.html)
* [Legacy Lighting](https://docs.unity3d.com/Manual/SL-Material.html)
* [Legacy Texture Combiners](https://docs.unity3d.com/Manual/SL-SetTexture.html)
* [Legacy BindChannels](https://docs.unity3d.com/Manual/SL-BindChannels.html)