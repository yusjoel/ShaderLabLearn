# Specular (Normal-Glossy.shader)

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 会在Unity3D中显示为目录结构
Shader "Legacy Shaders/Specular" {

// 属性, 可在Material面板中进行设置
Properties {
    _Color ("Main Color", Color) = (1,1,1,1)
    // 高光颜色
    _SpecColor ("Specular Color", Color) = (0.5, 0.5, 0.5, 1)
    // Shininess, 光泽度, 反光的区域的大小
    _Shininess ("Shininess", Range (0.01, 1)) = 0.078125
    // RGB是纹理, A是Gloss, 反光度, 0表示不反光, 1表示反光最强
    _MainTex ("Base (RGB) Gloss (A)", 2D) = "white" {}
}

SubShader {
    Tags { "RenderType"="Opaque" }
    LOD 300
    
CGPROGRAM
// 光照模型BlinnPhong
#pragma surface surf BlinnPhong

sampler2D _MainTex;
fixed4 _Color;
half _Shininess;

struct Input {
    float2 uv_MainTex;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
    o.Albedo = tex.rgb * _Color.rgb;
    // 反光度
    o.Gloss = tex.a;
    o.Alpha = tex.a * _Color.a;
    // 反射强度
    o.Specular = _Shininess;
}
ENDCG
}

Fallback "Legacy Shaders/VertexLit"
}

```

Lighting.cginc

```HLSL
// NOTE: some intricacy in shader compiler on some GLES2.0 platforms (iOS) needs 'viewDir' & 'h'
// to be mediump instead of lowp, otherwise specular highlight becomes too bright.
inline fixed4 UnityBlinnPhongLight (SurfaceOutput s, half3 viewDir, UnityLight light)
{
    // 计算half direction
    half3 h = normalize (light.dir + viewDir);
    // 计算散射光
    fixed diff = max (0, dot (s.Normal, light.dir));

    // 计算nh
    float nh = max (0, dot (s.Normal, h));
    // Specular乘128处理, 最后乘Gloss
    float spec = pow (nh, s.Specular*128.0) * s.Gloss;

    fixed4 c;
    // 最后使用散射光和反射光
    c.rgb = s.Albedo * light.color * diff + light.color * _SpecColor.rgb * spec;
    c.a = s.Alpha;

    return c;
}
```

> 备注: 与《Unity Shader入门精要》中有区别的是
Shininess， 也就是SurfaceOutput中的Specular，在该书示例Shader中叫做Gloss;
而作为Mask的Gloss, 也就是SurfaceOutput中的Gloss, 并没有使用.

* <https://docs.unity3d.com/Manual/shader-NormalSpecular.html>
* <https://docs.unity3d.com/Manual/SL-SurfaceShaders.html>
* <https://docs.unity3d.com/Manual/SL-SurfaceShaderLighting.html>
