# Specular (Normal-Glossy.shader)
* <https://docs.unity3d.com/Manual/shader-NormalSpecular.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
Shader "Legacy Shaders/Specular" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    // 高光颜色
    _SpecColor ("Specular Color", Color) = (0.5, 0.5, 0.5, 1)
    // Shininess, 光泽度, 反光的区域的大小
    _Shininess ("Shininess", Range (0.01, 1)) = 0.078125
    // RGB是纹理, A是Gloss, 反光度, 0表示不反光, 1表示反光最强
    _MainTex ("Base (RGB) Gloss (A)", 2D) = "white" {}
}

// SubShader, 必须
SubShader {
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }
    // 参考[LOD], 值越小适用范围越广
    LOD 300
    
// 参考[SurfaceShader]
// 表面着色代码也是写在CGPROGRAM - ENDCG块中, 但是包含在SubShader, 而不是Pass中

CGPROGRAM
// #pragma surface surfaceFunction lightModel [optionalparams]
// 指明表面着色器函数名 surf, 指明光照模型 BlinnPhong
// 内置了两个非物理光照模型 Lambert(散射) 和 BlinnPhong (镜面反射)
#pragma surface surf BlinnPhong

// 对应Properties中定义的属性
sampler2D _MainTex;
fixed4 _Color;
half _Shininess;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    // 采样
    fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
    o.Albedo = tex.rgb * _Color.rgb;
    // 反光度
    o.Gloss = tex.a;
    o.Alpha = tex.a * _Color.a;
    // 反射强度
    o.Specular = _Shininess;
}
ENDCG
}

// 参考[Fallback]
Fallback "Legacy Shaders/VertexLit"
}

```

## Lighting.cginc
可以看到BlinnPhong光照模型的计算

```HLSL
inline fixed4 UnityBlinnPhongLight (SurfaceOutput s, half3 viewDir, UnityLight light)
{
    // 计算half direction
    half3 h = normalize (light.dir + viewDir);
    // 计算漫反射系数
    fixed diff = max (0, dot (s.Normal, light.dir));

    // 计算nh
    float nh = max (0, dot (s.Normal, h));
    // Specular乘128处理, 最后乘Gloss
    float spec = pow (nh, s.Specular*128.0) * s.Gloss;

    fixed4 c;
    // 最后使用漫反射光和镜面反射光
    c.rgb = s.Albedo * light.color * diff + light.color * _SpecColor.rgb * spec;
    c.a = s.Alpha;

    return c;
}
```

> 备注: 与《Unity Shader入门精要》中有区别的是
Shininess， 也就是SurfaceOutput中的Specular，在该书示例Shader中叫做Gloss;
而作为Mask的Gloss, 也就是SurfaceOutput中的Gloss, 并没有使用.

* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
