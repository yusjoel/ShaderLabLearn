# Diffuse Detail (Normal-DiffuseDetail.shader)
* <https://docs.unity3d.com/Manual/shader-NormalDiffuseDetail.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
// 细节贴图, 在镜头推近的时候可以用细节贴图替代
Shader "Legacy Shaders/Diffuse Detail" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    _MainTex ("Base (RGB)", 2D) = "white" {}
    _Detail ("Detail (RGB)", 2D) = "gray" {}
}

// SubShader, 必须
SubShader {
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }
    // 参考[LOD], 值越小适用范围越广
    LOD 250

// 参考[SurfaceShader]
// 表面着色代码也是写在CGPROGRAM - ENDCG块中, 但是包含在SubShader, 而不是Pass中

CGPROGRAM
// #pragma surface surfaceFunction lightModel [optionalparams]
// 指明表面着色器函数名 surf, 指明光照模型 Lambert
// 内置了两个非物理光照模型 Lambert(散射) 和 BlinnPhong (镜面反射)
#pragma surface surf Lambert

// 对应Properties中定义的属性
sampler2D _MainTex;
sampler2D _Detail;
fixed4 _Color;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
    float2 uv_Detail;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    // 采样
    fixed4 c = tex2D(_MainTex, IN.uv_MainTex) * _Color;
    // unity_ColorSpaceDouble
    // 定义在UnityCG.cginc, 在线性空间时自动将2变为2^2.2
    c.rgb *= tex2D(_Detail,IN.uv_Detail).rgb * unity_ColorSpaceDouble.r;
    o.Albedo = c.rgb;
    o.Alpha = c.a;
}
ENDCG
}

// 参考[Fallback]
Fallback "Legacy Shaders/Diffuse"
}

```

## Lighting.cginc
可以看到Lambert散射光的计算

```HLSL
inline fixed4 UnityLambertLight (SurfaceOutput s, UnityLight light)
{
    fixed diff = max (0, dot (s.Normal, light.dir));

    fixed4 c;
    c.rgb = s.Albedo * light.color * diff;
    c.a = s.Alpha;
    return c;
}
```

## 说明
* 细节纹理(Detail Texture)是sRGB的颜色纹理, 需要在主纹理为白色时表现出自身的纹理内容
* 细节纹理可以令主纹理变亮或者变暗, 所以默认是灰色纹理, 在使用时最后会乘以2. 当细节纹理的颜色为灰色(0.5, 0.5, 0.5)时, 表示颜色不变, 小于0.5表示变暗, 大于0.5表示变亮
* 在线性空间时直接乘以2会令颜色变暗, 因为细节纹理的颜色会转到线性空间. 这里不能把细节纹理的sRGB选项去除, 需要把使用宏unity_ColorSpaceDouble来替换2
* 参考Catlike Coding - Unity - Tutorials - Rendering - [Combining Textures](https://catlikecoding.com/unity/tutorials/rendering/part-3/), [放牛的星星的翻译](https://zhuanlan.zhihu.com/p/138820408)

## 语法参考
* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)