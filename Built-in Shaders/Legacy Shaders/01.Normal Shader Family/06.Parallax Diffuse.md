# Parallax Diffuse (Normal-Parallax.shader)

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Parallax 视差, 在Bumped Diffuse的基础上增加视差效果
// 使用一个只带Alpha通道的Height Map, 用来加强法线纹理的效果
Shader "Legacy Shaders/Parallax Diffuse" {
Properties {
	_Color ("Main Color", Color) = (1,1,1,1)
	_Parallax ("Height", Range (0.005, 0.08)) = 0.02
	_MainTex ("Base (RGB)", 2D) = "white" {}
	_BumpMap ("Normalmap", 2D) = "bump" {}
	_ParallaxMap ("Heightmap (A)", 2D) = "black" {}
}

CGINCLUDE
sampler2D _MainTex;
sampler2D _BumpMap;
sampler2D _ParallaxMap;
fixed4 _Color;
float _Parallax;

struct Input {
	float2 uv_MainTex;
	float2 uv_BumpMap;
	// [Surface Shader input structure]
	// 视角, 这里第一次用到了这个参数
	// 尽管Specular的计算也要用到视角, 但计算是在UnityBlinnPhongLight中
	float3 viewDir;
};

void surf (Input IN, inout SurfaceOutput o) {
	// 和Bump Map公用一个uv
	half h = tex2D (_ParallaxMap, IN.uv_BumpMap).w;
	// 计算uv偏移
	float2 offset = ParallaxOffset (h, _Parallax, IN.viewDir);
	// 加到MainTex和BumpMap上
	IN.uv_MainTex += offset;
	IN.uv_BumpMap += offset;
	
	// 之后和Bumped Diffuse一样
	fixed4 c = tex2D(_MainTex, IN.uv_MainTex) * _Color;
	o.Albedo = c.rgb;
	o.Alpha = c.a;
	o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
}
ENDCG

SubShader {
	Tags { "RenderType"="Opaque" }
	LOD 500

	CGPROGRAM
	#pragma surface surf Lambert
	#pragma target 3.0
	ENDCG
}

SubShader {
	Tags { "RenderType"="Opaque" }
	LOD 500

	CGPROGRAM
	#pragma surface surf Lambert nodynlightmap
	ENDCG
}

FallBack "Legacy Shaders/Bumped Diffuse"
}
```

## UnityCG.cginc 

```HLGL
// Calculates UV offset for parallax bump mapping
inline float2 ParallaxOffset( half h, half height, half3 viewDir )
{
	// h是0-1, 转成-0.5-0.5
    h = h * height - height/2.0;
    float3 v = normalize(viewDir);
	// 根据视角计算偏移
    v.z += 0.42;
    return h * (v.xy / v.z);
}
```

* <https://docs.unity3d.com/Manual/shader-NormalParallaxDiffuse.html>
* [Surface Shader input structure](https://docs.unity3d.com/Manual/SL-SurfaceShaders.html)