# Parallax Diffuse (Normal-Parallax.shader)
* <https://docs.unity3d.com/Manual/shader-NormalParallaxDiffuse.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
// Parallax 视差, 在Bumped Diffuse的基础上增加视差效果
// 使用一个只带Alpha通道的Height Map, 用来加强法线纹理的效果
Shader "Legacy Shaders/Parallax Diffuse" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    _Parallax ("Height", Range (0.005, 0.08)) = 0.02
    _MainTex ("Base (RGB)", 2D) = "white" {}
    _BumpMap ("Normalmap", 2D) = "bump" {}
    _ParallaxMap ("Heightmap (A)", 2D) = "black" {}
}

// 参考[SurfaceShader]
// CGINCLUDE这个语法在官网上没有找到具体的说明
// 这里猜想是可以插入到下面的两个SubShader中
CGINCLUDE

// 对应Properties中定义的属性
sampler2D _MainTex;
sampler2D _BumpMap;
sampler2D _ParallaxMap;
fixed4 _Color;
float _Parallax;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
    float2 uv_BumpMap;
    // 视角
    float3 viewDir;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    // 采样, 和Bump Map公用一个uv
    half h = tex2D (_ParallaxMap, IN.uv_BumpMap).w;
    // 计算uv偏移
    float2 offset = ParallaxOffset (h, _Parallax, IN.viewDir);
    // 加到MainTex和BumpMap上
    IN.uv_MainTex += offset;
    IN.uv_BumpMap += offset;
    
    // 之后和Bumped Diffuse一样
    fixed4 c = tex2D(_MainTex, IN.uv_MainTex) * _Color;
    o.Albedo = c.rgb;
    o.Alpha = c.a;
    // 获取法线
    o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
}
ENDCG

// SubShader, 必须
SubShader {
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }
    // 参考[LOD], 值越小适用范围越广
    LOD 500

    CGPROGRAM
	// #pragma surface surfaceFunction lightModel [optionalparams]
	// 指明表面着色器函数名 surf, 指明光照模型 Lambert
	// 内置了两个非物理光照模型 Lambert(散射) 和 BlinnPhong (镜面反射)
    #pragma surface surf Lambert
    // 参考[Target]
    #pragma target 3.0
    ENDCG
}

SubShader {
    // 参考[Tags]
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags { "RenderType"="Opaque" }
    // 参考[LOD], 值越小适用范围越广
    LOD 500

    CGPROGRAM
	// #pragma surface surfaceFunction lightModel [optionalparams]
	// 指明表面着色器函数名 surf, 指明光照模型 Lambert
	// 内置了两个非物理光照模型 Lambert(散射) 和 BlinnPhong (镜面反射)
	// nodynlightmap - 关闭运行时动态全局光照
    #pragma surface surf Lambert nodynlightmap
    ENDCG
}

// 参考[Fallback]
FallBack "Legacy Shaders/Bumped Diffuse"
}

```

## Lighting.cginc
可以看到Lambert散射光的计算

```HLSL
inline fixed4 UnityLambertLight (SurfaceOutput s, UnityLight light)
{
    fixed diff = max (0, dot (s.Normal, light.dir));

    fixed4 c;
    c.rgb = s.Albedo * light.color * diff;
    c.a = s.Alpha;
    return c;
}
```

## UnityCG.cginc 
可以看到视差偏移的计算

```HLGL
// Calculates UV offset for parallax bump mapping
inline float2 ParallaxOffset( half h, half height, half3 viewDir )
{
    // h是0-1, 转成-0.5-0.5
    h = h * height - height/2.0;
    float3 v = normalize(viewDir);
    // 根据视角计算偏移
    v.z += 0.42;
    return h * (v.xy / v.z);
}
```


* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
* [Target](../../../ShaderLab%20Reference/Shader%20Compilation%20Target.md)
