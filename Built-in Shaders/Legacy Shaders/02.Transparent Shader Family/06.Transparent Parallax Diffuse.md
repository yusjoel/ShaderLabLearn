# Transparent Parallax Diffuse (Alpha-Parallax.shader)
* <https://docs.unity3d.com/Manual/shader-TransParallaxDiffuse.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
// Parallax 视差, 在Bumped Diffuse的基础上增加视差效果
// 使用一个只带Alpha通道的Height Map, 用来加强法线纹理的效果
Shader "Legacy Shaders/Transparent/Parallax Diffuse" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    _Parallax ("Height", Range (0.005, 0.08)) = 0.02
	// 纹理中多了Alpha通道
    _MainTex ("Base (RGB) Trans (A)", 2D) = "white" {}
    _BumpMap ("Normalmap", 2D) = "bump" {}
    _ParallaxMap ("Heightmap (A)", 2D) = "black" {}
}

// SubShader, 必须
SubShader {
    // 参考[Tags]
    // Queue: 和描绘顺序有关
    // IgnoreProjector: 忽略[Projector], 对于透明材质没有好的处理方法
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags {"Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent"}
    // 参考[LOD], 值越小适用范围越广
    LOD 500
    
// 参考[SurfaceShader]
// 表面着色代码也是写在CGPROGRAM - ENDCG块中, 但是包含在SubShader, 而不是Pass中

CGPROGRAM
// #pragma surface surfaceFunction lightModel [optionalparams]
// 指明表面着色器函数名 surf, 指明光照模型 Lambert
// 内置了两个非物理光照模型 Lambert(散射) 和 BlinnPhong (镜面反射)
// alpha分为alpha:blend, alpha:fade, alpha:premul具体区别待补充
#pragma surface surf Lambert alpha:fade

// 对应Properties中定义的属性
sampler2D _MainTex;
sampler2D _BumpMap;
sampler2D _ParallaxMap;
fixed4 _Color;
float _Parallax;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
    // Normal Map也另外使用了一套UV(Tiling & Offset)
    float2 uv_BumpMap;
    // 视角
    float3 viewDir;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    // 采样, 和Bump Map公用一个uv
    half h = tex2D (_ParallaxMap, IN.uv_BumpMap).w;
    // 计算uv偏移
    float2 offset = ParallaxOffset (h, _Parallax, IN.viewDir);
    // 加到MainTex和BumpMap上
    IN.uv_MainTex += offset;
    IN.uv_BumpMap += offset;
    
    // 之后和Bumped Diffuse一样
    fixed4 c = tex2D(_MainTex, IN.uv_MainTex) * _Color;
    o.Albedo = c.rgb;
    o.Alpha = c.a;
    // 获取法线
    o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
}
ENDCG
}

// 参考[Fallback]
FallBack "Legacy Shaders/Transparent/Bumped Diffuse"
}
```

## Lighting.cginc
可以看到Lambert散射光的计算

```HLSL
inline fixed4 UnityLambertLight (SurfaceOutput s, UnityLight light)
{
    fixed diff = max (0, dot (s.Normal, light.dir));

    fixed4 c;
    c.rgb = s.Albedo * light.color * diff;
    c.a = s.Alpha;
    return c;
}
```

## UnityCG.cginc 
可以看到视差偏移的计算

```HLGL
// Calculates UV offset for parallax bump mapping
inline float2 ParallaxOffset( half h, half height, half3 viewDir )
{
    // h是0-1, 转成-0.5-0.5
    h = h * height - height/2.0;
    float3 v = normalize(viewDir);
    // 根据视角计算偏移
    v.z += 0.42;
    return h * (v.xy / v.z);
}
```


* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
* [Projector](../../../ShaderLab%20Reference/Projector.md)
* [Target](../../../ShaderLab%20Reference/Shader%20Compilation%20Target.md)
