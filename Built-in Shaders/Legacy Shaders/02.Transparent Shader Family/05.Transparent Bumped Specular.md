# Transparent Bumped Specular (Alpha-BumpSpec.shader)
* <https://docs.unity3d.com/Manual/shader-TransBumpedSpecular.html>

```ShaderLab
// Unity built-in shader source. Copyright (c) 2016 Unity Technologies. MIT license (see license.txt)

// Shader名, 参考[Syntax], 会在Unity3D中显示为目录结构
// Bumped代表凹凸映射, 使用的具体方法是法线纹理(Normal Map)
// 另一种方法是高度纹理(Height Map), 实际使用中可以在Unity3D中直接转成Normal Map.
Shader "Legacy Shaders/Transparent/Bumped Specular" {

// 定义属性, 可在Material面板中进行设置, 参考[Properties]
Properties {
    // 属性名 ( 显示名称, 类型 ) = 默认值
    _Color ("Main Color", Color) = (1,1,1,1)
    // 高光颜色
    _SpecColor ("Specular Color", Color) = (0.5, 0.5, 0.5, 0)
    // Shininess, 光泽度, 反光的区域的大小
    _Shininess ("Shininess", Range (0.01, 1)) = 0.078125
    // RGB是纹理, A是即代表透明度也代表反光度, 0表示不反光, 1表示反光最强
    _MainTex ("Base (RGB) TransGloss (A)", 2D) = "white" {}
    _BumpMap ("Normalmap", 2D) = "bump" {}
}

// SubShader, 必须
SubShader {
    // 参考[Tags]
    // Queue: 和描绘顺序有关
    // IgnoreProjector: 忽略[Projector], 对于透明材质没有好的处理方法
    // RenderType: Shader的分类, 和[Shader Replacement]有关
    Tags {"Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent"}
    // 参考[LOD], 值越小适用范围越广
    LOD 400
    
// 参考[SurfaceShader]
// 表面着色代码也是写在CGPROGRAM - ENDCG块中, 但是包含在SubShader, 而不是Pass中

CGPROGRAM
// #pragma surface surfaceFunction lightModel [optionalparams]
// 指明表面着色器函数名 surf, 指明光照模型 BlinnPhong
// 内置了两个非物理光照模型 Lambert(散射) 和 BlinnPhong (镜面反射)
// alpha分为alpha:blend, alpha:fade, alpha:premul具体区别待补充
#pragma surface surf BlinnPhong alpha:fade
// 参考[Target]
#pragma target 3.0

// 对应Properties中定义的属性
sampler2D _MainTex;
sampler2D _BumpMap;
fixed4 _Color;
half _Shininess;

struct Input {
    // UV是自动生成的, 命名为uv(uv2) + 纹理名
    float2 uv_MainTex;
    float2 uv_BumpMap;
};

// 非物理光照模型都使用SurfaceOutput, 
// struct SurfaceOutput
// {
//     fixed3 Albedo;  // diffuse color
//     fixed3 Normal;  // tangent space normal, if written
//     fixed3 Emission;
//     half Specular;  // specular power in 0..1 range
//     fixed Gloss;    // specular intensity
//     fixed Alpha;    // alpha for transparencies
// };

void surf (Input IN, inout SurfaceOutput o) {
    // 采样
    fixed4 tex = tex2D(_MainTex, IN.uv_MainTex);
    o.Albedo = tex.rgb * _Color.rgb;
    // 反光度
    o.Gloss = tex.a;
    o.Alpha = tex.a * _Color.a;
    // 反射强度
    o.Specular = _Shininess;
	// 获取法线
    o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
}
ENDCG
}

// 参考[Fallback]
FallBack "Legacy Shaders/Transparent/VertexLit"
}

```

## Lighting.cginc
可以看到BlinnPhong散射光的计算

```HLSL
inline fixed4 UnityBlinnPhongLight (SurfaceOutput s, half3 viewDir, UnityLight light)
{
    // 计算half direction
    half3 h = normalize (light.dir + viewDir);
    // 计算散射光
    fixed diff = max (0, dot (s.Normal, light.dir));

    // 计算nh
    float nh = max (0, dot (s.Normal, h));
    // Specular乘128处理, 最后乘Gloss
    float spec = pow (nh, s.Specular*128.0) * s.Gloss;

    fixed4 c;
    // 最后使用散射光和反射光
    c.rgb = s.Albedo * light.color * diff + light.color * _SpecColor.rgb * spec;
    c.a = s.Alpha;

    return c;
}
```

> 备注: 与《Unity Shader入门精要》中有区别的是
Shininess， 也就是SurfaceOutput中的Specular，在该书示例Shader中叫做Gloss;
而作为Mask的Gloss, 也就是SurfaceOutput中的Gloss, 并没有使用.

* [Syntax](../../../ShaderLab%20Reference/ShaderLab%20Syntax.md)
* [Properties](../../../ShaderLab%20Reference/ShaderLab%20Properties.md)
* [Tags](../../../ShaderLab%20Reference/SubShader%20Tags.md)
* [LOD](../../../ShaderLab%20Reference/SubShader%20LOD.md)
* [SurfaceShader](../../../ShaderLab%20Reference/SurfaceShader.md)
* [Fallback](../../../ShaderLab%20Reference/ShaderLab%20Fallback.md)
* [Shader Replacement](../../../ShaderLab%20Reference/Shader%20Replacement.md)
* [Projector](../../../ShaderLab%20Reference/Projector.md)
* [Target](../../../ShaderLab%20Reference/Shader%20Compilation%20Target.md)
